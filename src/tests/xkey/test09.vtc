varnishtest "Test xkey vmod multiple keys, comma separated"
# https://github.com/varnish/varnish-modules/issues/102

server s1 {
	rxreq
	txresp -hdr "xkey: asdf, fdsa"

	rxreq
	txresp -hdr "xkey: qwer, rewq"
} -start

varnish v1 -vcl+backend {
	import xkey from "${vmod_builddir}/.libs/libvmod_xkey.so";

	sub vcl_recv {
		if (req.http.xkey-purge) {
			if (xkey.purge(req.http.xkey-purge) != 0) {
				return (synth(200, "Purged"));
			} else {
				return (synth(404, "No key"));
			}
		}
	}

	sub vcl_backend_response {
		set beresp.ttl = 60s;
		set beresp.grace = 0s;
		set beresp.keep = 0s;
	}

	sub vcl_synth {
		set resp.http.reason = resp.reason;
	}
} -start

client c1 {
	txreq
	rxresp
	txreq -url /2
	rxresp
} -run

varnish v1 -expect n_object == 2

client c1 {
	# Test the purge using the asdf key, so we check that we didn't include the comma
	txreq -hdr "xkey-purge: asdf"
	rxresp
	expect resp.status == 200
	expect resp.http.reason == "Purged"
} -run

delay 1

varnish v1 -expect n_object == 1

client c1 {
	txreq -hdr "xkey-purge: rewq"
	rxresp
	expect resp.status == 200
	expect resp.http.reason == "Purged"
} -run

delay 1

varnish v1 -expect n_object == 0
